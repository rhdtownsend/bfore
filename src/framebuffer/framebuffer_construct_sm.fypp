! Submodule : framebuffer_construct_sm
! Purpose   : Constructor routines for framebuffer_t

#:include 'forum.inc'

submodule (framebuffer_m) framebuffer_construct_sm

   ! No implicit typing

   implicit none (type, external)

   ! Procedures

contains

   module procedure framebuffer_t_data_

      @:CHECK_BOUNDS(SIZE(PL, 1), 3)
      @:CHECK_BOUNDS(SIZE(PL, 2), SIZE(PD, 1))
      @:CHECK_BOUNDS(SIZE(PL, 3), SIZE(PD, 2))

      @:CHECK_BOUNDS(SIZE(PF, 1), SIZE(PD, 1))
      @:CHECK_BOUNDS(SIZE(PF, 2), SIZE(PD, 2))

      ! Construct the framebuffer_t from the data

      fbuff%PD = PD
      fbuff%PL = PL
      fbuff%PF = PF

      fbuff%n_x = SIZE(PD, 1)
      fbuff%n_y = SIZE(PD, 2)

      ! Finish

      return

   end procedure framebuffer_t_data_

   !****

   module procedure framebuffer_t_dims_

      real(RD) :: PD(n_x,n_y)
      real(RD) :: PL(3,n_x,n_y)
      integer  :: PF(n_x,n_y)

      ! Construct the framebuffer_t from the dimensions

      PD = -HUGE(0._RD)
      PL = 0._RD
      PF = NULL_FACE

      fbuff = framebuffer_t_data_(PD, PL, PF)

      ! Finish

      return

   end procedure framebuffer_t_dims_

   !****

   module procedure framebuffer_t_merge_

      integer              :: n_x
      integer              :: n_y
      integer              :: i
      logical, allocatable :: mask(:,:)

      ! Construct the framebuffer_t by merging fbuffs

      n_x = MINVAL(fbuffs%n_x)
      n_y = MINVAL(fbuffs%n_y)

      @:ASSERT_DEBUG(ALL(fbuffs%n_x == n_x), 'mismatched dimensions')
      @:ASSERT_DEBUG(ALL(fbuffs%n_y == n_y), 'mismatched dimensions')

      fbuff = framebuffer_t(n_x, n_y)

      allocate(mask(n_x, n_y))

      fbuff_loop: do i = 1, SIZE(fbuffs)

         mask = fbuffs(i)%PD > fbuff%PD

         fbuff%PD = MERGE(fbuffs(i)%PD, fbuff%PD, mask)
         fbuff%PL = MERGE(fbuffs(i)%PL, fbuff%PL, SPREAD(mask, DIM=1, NCOPIES=3))
         fbuff%PF = MERGE(fbuffs(i)%PF, fbuff%PF, mask)

      end do fbuff_loop

      ! Finish

      return

   end procedure framebuffer_t_merge_

end submodule framebuffer_construct_sm
