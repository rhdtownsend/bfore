! Submodule : framebuffer_io_sm
! Purpose   : Input/output routines for framebuffer_t

#:include 'forum.inc'

submodule (framebuffer_m) framebuffer_io_sm

   ! Uses

   use file_m

   ! No implicit typing

   implicit none (type, external)

   ! Procedures

contains

   module procedure read

      integer :: revision

      ! Read the framebuffer_t

      call check_type(hdf5io, 'framebuffer_t', stat)
      if (PRESENT(stat)) then
         if (stat /= STAT_OK) return
      end if

      call hdf5io%read_attr('REVISION', revision)

      select case(revision)
      case(1)
         call read_rev1_()
      case default
         if (PRESENT(stat)) then
            stat = STAT_INVALID_GROUP_REVISION
            return
         else
            @:ABORT('invalid group revision')
         end if
      end select

      ! Finish

      return

   contains

      subroutine read_rev1_()

         integer               :: n_x
         integer               :: n_y
         real(RD), allocatable :: PZ(:,:)
         real(RD), allocatable :: PL(:,:,:)
         integer, allocatable  :: PO(:,:)

         call hdf5io%read_attr('n_x', n_x)
         call hdf5io%read_attr('n_y', n_y)

         call hdf5io%alloc_read_dset('pixel_depths', PZ)
         call hdf5io%alloc_read_dset('pixel_lambdas', PL)
         call hdf5io%alloc_read_dset('pixel_origins', PO)

         if (.NOT. &
            ALL(SHAPE(PZ) == [n_x, n_y]) .AND. &
            ALL(SHAPE(PL) == [3, n_x, n_y]) .AND. &
            ALL(SHAPE(PO) == [n_x, n_y])) then
            if (PRESENT(stat)) then
               stat = STAT_INVALID_DATASET_DIM
               return
            else
               @:ABORT('invalid dataset dimension')
            end if
         end if

         select type(self)
         type is(framebuffer_t)
            self = framebuffer_t_data_(PZ, PL, PO)
         class default
            @:ABORT('invalid type')
         end select

         ! Finish

         if (PRESENT(stat)) stat = STAT_OK

         return

      end subroutine read_rev1_

   end procedure read

   !****

   module procedure write

      ! Write the framebuffer_t

      call hdf5io%write_attr('TYPE', 'framebuffer_t')
      call hdf5io%write_attr('REVISION', 1)

      call hdf5io%write_attr('n_x', self%n_x)
      call hdf5io%write_attr('n_y', self%n_y)

      call hdf5io%write_dset('pixel_depths', self%PZ)
      call hdf5io%write_dset('pixel_lambdas', self%PL)
      call hdf5io%write_dset('pixel_origins', self%PO)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end procedure write

end submodule framebuffer_io_sm
