! Submodule : map_m
! Purpose   : Rendering routines for map_t

#:include 'forum.inc'

submodule (map_m) map_render_sm

   ! Uses

   use framebuffer_m
   use transform_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Procedures

contains

   module procedure render

      type(transform_t)   :: tform
      integer             :: v
      real(RD)            :: VV(3,self%n_v,mesh%n_vert)
      type(framebuffer_t) :: fbuff
      integer             :: i_a

      integer :: start, end, count

      @:CHECK_BOUNDS(SIZE(vert_scalars, 1), self%n_s)
      @:CHECK_BOUNDS(SIZE(vert_scalars, 2), mesh%n_vert)

      @:CHECK_BOUNDS(SIZE(vert_vectors, 1), 3)
      @:CHECK_BOUNDS(SIZE(vert_vectors, 2), self%n_v)
      @:CHECK_BOUNDS(SIZE(vert_vectors, 3), mesh%n_vert)

      ! Set up transformations

      tform = view%camera_transform()

      ! Transform the vertex attribute vectors to the camera system

      vert_loop: do v = 1, mesh%n_vert
         do i_a = 1, self%n_v
            VV(:,i_a,v) = tform%vector_image(vert_vectors(:,i_a,v))
         end do
      end do vert_loop

      ! Initialize the framebuffer

      fbuff = framebuffer_t(self%n_x, self%n_y)

      ! Render the mesh into the framebuffer

      call system_clock(start, count)

      call fbuff%render(mesh, view, process_pixel_)

      call system_clock(end)

      print *,'render:', REAL(end-start, RD)/count

      ! Finish

      return

   contains

      subroutine process_pixel_(i_x, i_y, lambda, origin)

         integer, intent(in)  :: i_x
         integer, intent(in)  :: i_y
         real(RD), intent(in) :: lambda(3)
         integer, intent(in)  :: origin

         integer :: f_e
         integer :: v1
         integer :: v2
         integer :: v3
         integer :: i_a

         ! Determine the origin vertices

         f_e = mesh%face_edge(origin)

         v1 = mesh%edge_vert(f_e)
         v2 = mesh%edge_vert(mesh%edge_next(f_e))
         v3 = mesh%edge_vert(mesh%edge_prev(f_e))

         ! Update the attributes

         do i_a = 1, self%n_s
            self%PS(i_a,i_x,i_y) = DOT_PRODUCT(vert_scalars(i_a,[v1, v2, v3]), lambda)
         end do

         do i_a = 1, self%n_v
            self%PV(:,i_a,i_x,i_y) = MATMUL(VV(:,i_a,[v1, v2, v3]), lambda)
         end do

         ! Finish

         return

      end subroutine process_pixel_

   end procedure render

end submodule map_render_sm
