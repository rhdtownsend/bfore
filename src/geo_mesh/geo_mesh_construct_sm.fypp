! Submodule : geo_mesh_construct_sm
! Purpose   : Constructors for geo_mesh_t types

#:include 'forum.inc'

submodule (geo_mesh_m) geo_mesh_construct_sm

   ! Uses

   use forum_m

   use mesh_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Procedures

contains

   module procedure geo_mesh_t_vert_coords_

      @:CHECK_BOUNDS(SIZE(vert_coords, 1), 3)
      @:CHECK_BOUNDS(SIZE(vert_coords, 2), mesh%n_vert)

      ! Construct the geo_mesh from the supplied mesh and
      ! vertex-coordinate list

      geo_mesh%mesh_t = mesh

      geo_mesh%VC = vert_coords

      ! Finish

      return

   end procedure geo_mesh_t_vert_coords_

   !****

   module procedure geo_mesh_t_geo_meshes_

      integer               :: n_vert
      real(RD), allocatable :: VC(:,:)
      integer               :: m
      integer               :: v1
      integer               :: v2
   
      @:ASSERT_DEBUG(SIZE(geo_meshes) >= 1)

      ! Construct the geo_mesh_t by concatenating the geo_meshes

      n_vert = SUM(geo_meshes%n_vert)

      ! Set up concatenated tables

      allocate(VC(3,n_vert))

      geo_mesh_loop: do m = 1, SIZE(geo_meshes)

         v1 = SUM(geo_meshes(:m-1)%n_vert) + 1
         v2 = v1 + geo_meshes(m)%n_vert - 1

         VC(:,v1:v2) = geo_meshes(m)%VC

      end do geo_mesh_loop

      ! Create the geo_mesh_t

      geo_mesh = geo_mesh_t(mesh_t(geo_meshes%mesh_t), VC)

      ! Finish

      return

   end procedure geo_mesh_t_geo_meshes_

end submodule geo_mesh_construct_sm

