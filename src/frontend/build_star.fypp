! Program : build_star
! Purpose : Build a meshes representing a star

#:include 'forum.inc'

program build_star

   ! Uses

   use forum_m

   use mesh_m
   use star_m
   use stat_m

   use tomlf

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Variables

   character(:), allocatable :: file_name

   real(RD)                  :: M
   real(RD)                  :: R_pole
   real(RD)                  :: T_pole
   real(RD)                  :: omega
   real(RD)                  :: beta
   integer                   :: n_refine
   character(:), allocatable :: star_file_name

   type(toml_table), allocatable  :: table
   type(toml_context)             :: context
   type(toml_error), allocatable  :: error

   type(mesh_t)   :: mesh
   type(star_t)   :: star
   type(hdf5io_t) :: hdf5io

   ! Read command-line arguments

   @:ASSERT(n_arg() == 1, 'Syntax: build_star <file_name>')

   call get_arg(1, file_name)

   ! Read the input file & initialize

   call toml_load(table, file_name, error=error)
   if (ALLOCATED(error)) then
      print *, error%message
      stop 1
   end if

   call init_star(table, M, R_pole, T_pole, omega)
   call init_physics(table, beta)
   call init_mesh(table, n_refine)
   call init_files(table, star_file_name)

   call table%destroy()

   ! Create the star

   mesh = star_mesh(omega, beta, n_refine)

   star = star_t(mesh, M, R_pole, T_pole, omega)

   ! Write the star

   hdf5io = hdf5io_t(star_file_name, CREATE_FILE)
   call star%write(hdf5io)
   call hdf5io%final()

   ! Finish

contains

   subroutine init_star(table, M, R_pole, T_pole, omega)

      type(toml_table), intent(inout) :: table
      real(RD), intent(out)           :: M
      real(RD), intent(out)           :: R_pole
      real(RD), intent(out)           :: T_pole
      real(RD), intent(out)           :: omega

      type(toml_table), pointer :: table_star
      integer                   :: stat
      integer                   :: origin

      ! Read the star parameters table

      call get_value(table, 'star', table_star, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'star', origin, 'table')

      call get_value(table_star, 'M', M, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'M', origin, 'real')

      call get_value(table_star, 'R_pole', R_pole, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'R_pole', origin, 'real')

      call get_value(table_star, 'T_pole', T_pole, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'T_pole', origin, 'real')

      call get_value(table_star, 'omega', omega, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'omega', origin, 'real')

      M = M*M_SUN
      R_pole = R_pole*R_sun

      ! Finish

      return

   end subroutine init_star

   !****

   subroutine init_physics(table, beta)

      type(toml_table), intent(inout) :: table
      real(RD), intent(out)           :: beta

      type(toml_table), pointer :: table_physics
      integer                   :: stat
      integer                   :: origin

      ! Read the physics parameters table

      call get_value(table, 'physics', table_physics, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'physics', origin, 'table')

      call get_value(table_physics, 'beta', beta, 0.25_RD, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'beta', origin, 'real')

      ! Finish

      return

   end subroutine init_physics

   !****

   subroutine init_mesh(table, n_refine)

      type(toml_table), intent(inout) :: table
      integer, intent(out)            :: n_refine

      type(toml_table), pointer :: table_mesh
      integer                   :: stat
      integer                   :: origin

      ! Read the mesh parameters table

      call get_value(table, 'mesh', table_mesh, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'mesh', origin, 'table')

      call get_value(table_mesh, 'n_refine', n_refine, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'n_refine', origin, 'integer')

      ! Finish

      return

   end subroutine init_mesh

   !****

   subroutine init_files(table, star_file_name)

      type(toml_table), intent(inout)        :: table
      character(:), allocatable, intent(out) :: star_file_name

      type(toml_table), pointer :: table_files
      integer                   :: stat
      integer                   :: origin

      ! Read the files parameters table

      call get_value(table, 'files', table_files, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'files', origin, 'table')

      call get_value(table_files, 'star_file_name', star_file_name, stat=stat, origin=origin)
      call check_toml_stat(context, stat, 'star_file_name', origin, 'string')

      ! Finish

      return

   end subroutine init_files

end program build_star
