! Submodule : mesh_geometry_sm
! Purpose   : Geometry-related routines for mesh_t types

#:include 'forum.inc'

submodule (mesh_m) mesh_geometry_sm

   ! Uses

   use forum_m

   use math_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Procedures

contains

   module procedure vert_point

      @:ASSERT_DEBUG(v >= 1 .AND. v <= self%n_vert, 'invalid vertex')

      ! Get the point (coordinates) associated with vertex v. Can also
      ! be used to set the point

      P => self%VP(:,v)

      ! Finish

      return

   end procedure vert_point

   !****

   module procedure face_area

      integer  :: f_e
      integer  :: v1
      integer  :: v2
      integer  :: v3
      real(RD) :: P1(3)
      real(RD) :: P2(3)
      real(RD) :: P3(3)

      ! Get the area vector of face f

      f_e = self%face_edge(f)

      v1 = self%edge_vert(f_e)
      v2 = self%edge_vert(self%edge_next(f_e))
      v3 = self%edge_vert(self%edge_prev(f_e))

      P1 = self%vert_point(v1)
      P2 = self%vert_point(v2)
      P3 = self%vert_point(v3)

      A = 0.5_RD*cross_product(P2-P1, P3-P1)

      ! Finish

      return

   end procedure face_area

end submodule mesh_geometry_sm
