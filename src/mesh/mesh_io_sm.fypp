! Submodule : mesh_io_sm
! Purpose   : Input/output routines for mesh_t

#:include 'forum.inc'

submodule (mesh_m) mesh_io_sm

   ! Uses

   use forum_m

   use file_m
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none (type, external)

   ! Procedures

contains

   module procedure read

      integer :: revision

      ! Read the mesh_t

      call check_type(hdf5io, 'mesh_t', stat)
      if (PRESENT(stat)) then
         if (stat /= STAT_OK) return
      end if

      call hdf5io%read_attr('REVISION', revision)

      select case(revision)
      case(1)
         call read_rev1_()
      case default
         if (PRESENT(stat)) then
            stat = STAT_INVALID_GROUP_REVISION
            return
         else
            @:ABORT('invalid group revision')
         end if
      end select

      ! Finish

      return

   contains

      subroutine read_rev1_()

         integer               :: n_vert
         integer               :: n_edge
         integer               :: n_face
         integer, allocatable  :: EV(:)
         integer, allocatable  :: ET(:)
         integer, allocatable  :: VE(:)
         real(RD), allocatable :: VP(:,:)

         call hdf5io%read_attr('n_vert', n_vert)
         call hdf5io%read_attr('n_edge', n_edge)
         call hdf5io%read_attr('n_face', n_face)

         call hdf5io%alloc_read_dset('edge_verts', EV)
         call hdf5io%alloc_read_dset('edge_twins', ET)
         call hdf5io%alloc_read_dset('vert_edges', VE)
         call hdf5io%alloc_read_dset('vert_points', VP)

         if (.NOT. &
            ALL(SHAPE(EV) == [n_edge]) .AND. &
            ALL(SHAPE(ET) == [n_edge]) .AND. &
            ALL(SHAPE(VE) == [n_vert]) .AND. &
            ALL(SHAPE(VP) == [3, n_vert])) then
            if (PRESENT(stat)) then
               stat = STAT_INVALID_DATASET_DIM
               return
            else
               @:ABORT('invalid dataset dimension')
            end if
         end if

         select type(self)
         type is(mesh_t)
            self = mesh_t_data_(EV, ET, VE, VP)
         class default
            @:ABORT('invalid type')
         end select

         ! Finish

         if (PRESENT(stat)) stat = STAT_OK

         return

      end subroutine read_rev1_

   end procedure read

   !****

   module procedure write

      ! Write the mesh_t

      call hdf5io%write_attr('TYPE', 'mesh_t')
      call hdf5io%write_attr('REVISION', 1)

      call hdf5io%write_attr('n_vert', self%n_vert)
      call hdf5io%write_attr('n_edge', self%n_edge)
      call hdf5io%write_attr('n_face', self%n_face)

      call hdf5io%write_dset('edge_verts', self%EV)
      call hdf5io%write_dset('edge_twins', self%ET)
      call hdf5io%write_dset('vert_edges', self%VE)
      call hdf5io%write_dset('vert_points', self%VP)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end procedure write

end submodule mesh_io_sm
